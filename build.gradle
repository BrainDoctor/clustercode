plugins {
    id "org.unbroken-dome.test-sets" version "1.4.2"
    id "java"
    id "idea"
    id "jacoco"
    id "com.moowork.node" version "1.2.0"
    id "com.benjaminsproule.swagger" version "0.1.2"
}

group "net.chrigel"
version "1.3.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

ext {
    guiceVersion = "4.1.0"
    log4jVersion = "2.8.+"
    jacksonVersion = "2.9.+"
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {

    // Core dependencies
    compile("org.squirrelframework:squirrel-foundation:0.3.+") {
        exclude group: "log4j"
        exclude group: "org.slf4j"
    }
    compile "org.jgroups:jgroups:4.0.+"

    // REST dependencies
    compile "io.logz:guice-jersey:1.0.+"
    compile "com.owlike:genson:1.+"

    // compiler helpers
    compileOnly "org.projectlombok:lombok:1.16.+"
    compileOnly "io.swagger:swagger-annotations:1.5.+"
    // DI frameworks
    compile "javax.inject:javax.inject:1"
    compile "com.google.inject.extensions:guice-multibindings:${guiceVersion}"

    // logging
    compile "org.slf4j:slf4j-ext:1.7.+"
    runtime "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    runtime "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    runtime "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

    // testing
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:2.0.+"
    testCompile "com.google.jimfs:jimfs:1.+"
    testCompile "org.assertj:assertj-core:3.8.0"

    integrationTestCompile sourceSets.test.output

}

swagger {
    apiSource {
        locations = ["net.chrigel.clustercode.api"]
        schemes = ["http"]
        host = "your.clustercode.domain:8080"
        basePath = "/api/v1"
        info {
            title = "Clustercode REST API"
            version = "1.3.0"
            description = "Convert your videos in a cluster!"
        }

        templatePath = "${project.rootDir}/src/swagger/strapdown.html.hbs"
        outputPath = "${project.buildDir}/swagger/swagger.html"
        swaggerDirectory = "${project.buildDir}/swagger"
    }
}

task fullBuild(type: Jar) {
    manifest {
        attributes "Main-Class": "net.chrigel.clustercode.Startup",
            "Implementation-Version": version,
            "Implementation-Title": project.name
    }
    archiveName = "clustercode.jar"
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine "echo", "Downloaded all dependencies"
}

task windowsZip(type: Zip) {
    from("${project.buildDir}/libs/${fullBuild.archiveName}") { into "clustercode" }
    from("src/assembly/clustercode/resources") { into "clustercode" }
    from("src/assembly/clustercode/windows") { into "clustercode" }
    from("dist") { into "clustercode/clustercode-admin" }
    from("src/assembly/clustercode-admin/windows") { into "clustercode" }
    from("src/assembly/clustercode-admin/nginx") { into "clustercode/nginx/conf" }
    from("build/swagger") { into "clustercode/clustercode-admin/static" }
    classifier "win"
}

task adminBuild(type: NpmTask) {
    args = ["run", "build"]
}

task adminZip(type: Zip) {
    from("dist") { into "clustercode-admin" }
    from("build/swagger") { into "clustercode-admin/static" }
    baseName "clustercode-admin"
}

task githubRelease()

fullBuild.dependsOn test

windowsZip.dependsOn fullBuild
windowsZip.dependsOn adminBuild
windowsZip.dependsOn generateSwaggerDocumentation

check.dependsOn integrationTest
integrationTest.mustRunAfter test

adminZip.dependsOn adminBuild
adminZip.dependsOn generateSwaggerDocumentation

githubRelease.dependsOn windowsZip
githubRelease.dependsOn adminZip

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

jacoco {
    reportsDir file("${project.buildDir}/reports/jacoco")
    toolVersion "0.7.6.201602180812"
}

