plugins {
    id "org.unbroken-dome.test-sets" version "1.4.2"
    id "java"
    id "idea"
    id "jacoco"
    id "com.benjaminsproule.swagger" version "0.1.2"
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group "clustercode"
version "1.4.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

ext {
    guiceVersion = "4.1.0"
    log4jVersion = "2.8.+"
    jacksonVersion = "2.9.+"
    dep_owner = "org.aeonbits.owner:owner:1.0.10"
    dep_rxjava = "io.reactivex.rxjava2:rxjava:2.1.+"
    dep_jgroups = "org.jgroups:jgroups:4.0.+"
    dep_inject = "javax.inject:javax.inject:1"
}

allprojects {
    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }
}

subprojects {
    group = 'clustercode'
    version = '1.0'

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    apply plugin: "java"

    repositories {
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.16.+"

        // logging
        compile "org.slf4j:slf4j-ext:1.7.+"
        runtime "org.apache.logging.log4j:log4j-api:${log4jVersion}"
        runtime "org.apache.logging.log4j:log4j-core:${log4jVersion}"
        runtime "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

        // testing
        testRuntime "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
        testCompile "junit:junit:4.12"
        testCompile "org.mockito:mockito-all:2.0.+"
        testCompile "com.google.jimfs:jimfs:1.+"
        testCompile "org.assertj:assertj-core:3.8.0"
    }
}

dependencies {

    // Core dependencies
    compile("org.squirrelframework:squirrel-foundation:0.3.+") {
        exclude group: "log4j"
        exclude group: "org.slf4j"
    }
    compile "org.jgroups:jgroups:4.0.+"
    compile 'com.google.guava:guava:23.5-jre'
    compile "io.reactivex.rxjava2:rxjava:2.1.+"

    // REST dependencies
    compile "io.logz:guice-jersey:1.0.+"
    compile "com.owlike:genson:1.+"

    // compiler helpers
    compileOnly "org.projectlombok:lombok:1.16.+"
    compileOnly "io.swagger:swagger-annotations:1.5.+"
    // DI frameworks
    compile "com.google.inject.extensions:guice-multibindings:${guiceVersion}"


    // testing
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:2.0.+"
    testCompile "com.google.jimfs:jimfs:1.+"
    testCompile "org.assertj:assertj-core:3.8.0"

    integrationTestCompile sourceSets.test.output
    compile project(":clustercode.api.domain")
    compile project(":clustercode.impl.constraint")

}

swagger {
    apiSource {
        locations = ["net.chrigel.clustercode.api"]
        schemes = ["http"]
        host = "your.clustercode.domain:8080"
        basePath = "/api/v1"
        info {
            title = "Clustercode REST API"
            version = "1.3.0"
            description = "Convert your videos in a cluster!"
        }

        templatePath = "${project.rootDir}/src/swagger/strapdown.html.hbs"
        outputPath = "${project.buildDir}/swagger/swagger.html"
        swaggerDirectory = "${project.buildDir}/swagger"
    }
}

shadowJar {
    dependencies {
        exclude(project(":clustercode.test.util"))
    }
}

task fullBuild(type: Jar) {
    manifest {
        attributes "Main-Class": "net.chrigel.clustercode.Startup",
            "Implementation-Version": version,
            "Implementation-Title": project.name
    }
    archiveName = "clustercode.jar"
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine "echo", "Downloaded all dependencies"
}

task windowsZip(type: Zip) {
    from("${project.buildDir}/libs/${fullBuild.archiveName}") { into "clustercode" }
    from("src/assembly/clustercode/resources") { into "clustercode" }
    from("src/assembly/clustercode/windows") { into "clustercode" }
    from("dist") { into "clustercode/clustercode-admin" }
    from("src/assembly/clustercode-admin/windows") { into "clustercode" }
    from("src/assembly/clustercode-admin/nginx") { into "clustercode/nginx/conf" }
    from("build/swagger") { into "clustercode/clustercode-admin/static" }
    classifier "win"
}

task githubRelease()

fullBuild.dependsOn test

windowsZip.dependsOn fullBuild
windowsZip.dependsOn generateSwaggerDocumentation

check.dependsOn integrationTest
integrationTest.mustRunAfter test

githubRelease.dependsOn windowsZip

jacoco {
    reportsDir file("${project.buildDir}/reports/jacoco")
    toolVersion "0.7.6.201602180812"
}

